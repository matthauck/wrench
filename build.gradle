apply plugin: 'java'

sourceCompatibility = 1.8

group = "com.github.matthauck"
version = '0.1-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {

    compile 'org.apache.commons:commons-dbcp2:2.0.1'

    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile 'com.h2database:h2:1.3.176'
    testCompile 'org.flywaydb:flyway-core:3.0'

}

test {

  outputs.upToDateWhen { false }

  testLogging {
    exceptionFormat "full"
    showStandardStreams true
    events "passed", "skipped", "failed"
  }
}

//// stuff for uploading to maven 
apply plugin: 'maven'
apply plugin: 'signing'


task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar, javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name 'wrench'
        packaging 'jar'
        // optionally artifactId can be defined here 
        description 'a simple orm for java'
        url 'https://github.com/matthauck/wrench'

        scm {
          connection 'scm:svn:https://github.com/matthauck/wrench'
          developerConnection 'scm:svn:https://github.com/matthauck/wrench'
          url 'https://github.com/matthauck/wrench'
        }

        licenses {
          license {
            name 'MIT'
            url 'http://www.opensource.org/licenses/MIT'
          }
        }

        developers {
          developer {
            id 'matthauck'
            name 'Matt Hauck'
            email 'matthauck@gmail.com'
          }
        }
      }
    }
  }
}

